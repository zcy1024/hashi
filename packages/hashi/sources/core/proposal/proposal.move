// Copyright (c) Mysten Labs, Inc.
// SPDX-License-Identifier: Apache-2.0

/// Generic quorum-voting machinery shared by every governance action. A
/// `Proposal<T>` wraps a typed payload `T` (defined by the proposal-type
/// modules under `types/`) together with the votes it has gathered; committee
/// members vote by weight, and once the proposal's quorum threshold is reached
/// it can be executed exactly once, releasing the payload to the executing
/// module and archiving the proposal. Proposals expire after seven days, after
/// which unexecuted ones may be deleted.
///
/// Visibility note: every proposal-type module exposes `propose` and
/// `execute` as private `entry` functions, as are `vote`, `remove_vote` and
/// `delete_expired` here. A `public` signature is frozen at publish by Sui's
/// compatible-upgrade check, an `entry` one may change in any later upgrade,
/// and no other Move package has a use for these: proposals are authorized
/// by the sender's registration, not by a calling package. PTBs still build
/// the `VecMap` and `Value` arguments with public calls, whose results have
/// `drop` and `store` and are therefore never hot arguments under the
/// private-entry rules. `upgrade::execute` hands back an `UpgradeTicket`
/// and a hot potato that the same PTB's `Upgrade` command and
/// `finalize_upgrade` consume; until then no other private entry may take
/// `Hashi` in that PTB, which the upgrade PTB never does.
module hashi::proposal;

use hashi::{hashi::Hashi, threshold};
use std::string::String;
use sui::{clock::Clock, vec_map::VecMap};

// ~~~~~~~ Constants ~~~~~~~

const MAX_PROPOSAL_DURATION_MS: u64 = 1000 * 60 * 60 * 24 * 7; // 7 days

// ~~~~~~~ Errors ~~~~~~~

#[error(code = 0)]
const EUnauthorizedCaller: vector<u8> = b"Caller must be a voting member";
#[error(code = 1)]
const EVoteAlreadyCounted: vector<u8> = b"Vote already counted";
#[error(code = 2)]
const EQuorumNotReached: vector<u8> = b"Quorum not reached";
#[error(code = 3)]
const ENoVoteFound: vector<u8> = b"Vote doesn't exist";
#[error(code = 4)]
const EProposalNotExpired: vector<u8> = b"Proposal not expired";
#[error(code = 5)]
const EProposalExpired: vector<u8> = b"Proposal expired";
#[error(code = 6)]
const EProposalAlreadyExecuted: vector<u8> = b"Proposal already executed";
#[error(code = 7)]
const ENotCommitteeMember: vector<u8> = b"Validator is not a member of the current committee";
#[error(code = 8)]
const ENoCommittee: vector<u8> =
    b"No committee exists for the current epoch; votes cannot be cast before genesis";

// ~~~~~~~ Structs ~~~~~~~

public struct Proposal<T> has key, store {
    id: UID,
    creator: address,
    votes: vector<address>,
    quorum_threshold_bps: u64,
    created_timestamp_ms: u64,
    /// Clock timestamp at execution. `None` until the proposal executes.
    executed_timestamp_ms: Option<u64>,
    metadata: VecMap<String, String>,
    data: T,
}

// ~~~~~~~ Events ~~~~~~~

public struct ProposalCreated<phantom T> has copy, drop {
    proposal_id: ID,
    timestamp_ms: u64,
}

public struct VoteCast<phantom T> has copy, drop {
    proposal_id: ID,
    voter: address,
}

public struct VoteRemoved<phantom T> has copy, drop {
    proposal_id: ID,
    voter: address,
}

public struct ProposalDeleted<phantom T> has copy, drop {
    proposal_id: ID,
}

public struct ProposalExecuted<T> has copy, drop {
    proposal_id: ID,
    data: T,
}

public struct QuorumReached<phantom T> has copy, drop {
    proposal_id: ID,
}

// ~~~~~~~ Entry Functions ~~~~~~~

// `ctx` stays `&mut` (here and in `remove_vote`) so future versions can
// create objects without a signature change.
#[allow(unused_mut_parameter)]
entry fun vote<T: store>(
    hashi: &mut Hashi,
    validator_address: address,
    proposal_id: ID,
    clock: &Clock,
    ctx: &mut TxContext,
) {
    hashi.versioning().assert_version_enabled();
    assert!(hashi.committee_set().member_authorized(validator_address, ctx), EUnauthorizedCaller);
    // Before genesis no committee exists yet, so there is nothing to weigh a
    // vote against. Refuse by name rather than letting the committee lookup
    // below abort inside the bag.
    assert!(hashi.committee_set().has_committee(hashi.committee_set().epoch()), ENoCommittee);
    // Registration authorizes the key; only current-committee membership
    // carries weight. A registered validator outside the committee (rotated
    // out, or not yet seated) must not record a weightless vote.
    assert!(hashi.current_committee().has_member(&validator_address), ENotCommitteeMember);

    let proposal: &mut Proposal<T> = hashi.proposals_mut().active_mut().borrow_mut(proposal_id);

    assert!(!proposal.votes.contains(&validator_address), EVoteAlreadyCounted);
    assert!(!proposal.is_expired(clock), EProposalExpired);

    proposal.votes.push_back(validator_address);

    sui::event::emit(VoteCast<T> { proposal_id, voter: validator_address });
    if (proposal.quorum_reached(hashi)) {
        sui::event::emit(QuorumReached<T> { proposal_id });
    }
}

#[allow(unused_mut_parameter)]
entry fun remove_vote<T: store>(
    hashi: &mut Hashi,
    validator_address: address,
    proposal_id: ID,
    ctx: &mut TxContext,
) {
    hashi.versioning().assert_version_enabled();
    assert!(hashi.committee_set().member_authorized(validator_address, ctx), EUnauthorizedCaller);

    let proposal: &mut Proposal<T> = hashi.proposals_mut().active_mut().borrow_mut(proposal_id);
    let index = proposal
        .votes
        .find_index!(|v| v == &validator_address)
        .destroy_or!(abort ENoVoteFound);

    proposal.votes.remove(index);
    sui::event::emit(VoteRemoved<T> {
        proposal_id: proposal.id.to_inner(),
        voter: validator_address,
    });
}

/// Delete an expired, unexecuted proposal. Permissionless: an expired
/// proposal can be neither voted nor executed, so nothing is lost. The
/// payload is returned for the caller to discard; the `drop` bound states
/// what every proposal payload already has (`execute` requires it too).
entry fun delete_expired<T: drop + store>(hashi: &mut Hashi, proposal_id: ID, clock: &Clock): T {
    hashi.versioning().assert_version_enabled();
    // Executed proposals are archived in the executed bag and must
    // never be deletable, even after they expire. Refuse explicitly so
    // the caller gets `EProposalAlreadyExecuted` instead of the bag's
    // missing-key abort.
    assert!(!hashi.proposals().executed().contains(proposal_id), EProposalAlreadyExecuted);
    let proposal: Proposal<T> = hashi.proposals_mut().active_mut().remove(proposal_id);

    assert!(proposal.is_expired(clock), EProposalNotExpired);
    sui::event::emit(ProposalDeleted<T> { proposal_id });
    proposal.delete()
}

// ~~~~~~~ Package Functions ~~~~~~~

public(package) fun create<T: store>(
    hashi: &mut Hashi,
    validator_address: address,
    data: T,
    quorum_threshold_bps: u64,
    metadata: VecMap<String, String>,
    clock: &Clock,
    ctx: &mut TxContext,
): ID {
    // The caller must be the committee member `validator_address`, or the
    // operator key it has delegated to. The vote is recorded under
    // `validator_address` so quorum weight is computed correctly.
    assert!(hashi.committee_set().member_authorized(validator_address, ctx), EUnauthorizedCaller);
    // Before genesis no committee exists yet: the registered validators are
    // the future committee and registration is the only authority. Once one
    // exists, the same membership rule as `vote` applies to the creator's
    // automatic vote.
    let has_committee = {
        let committee_set = hashi.committee_set();
        committee_set.has_committee(committee_set.epoch())
    };
    assert!(
        !has_committee || hashi.current_committee().has_member(&validator_address),
        ENotCommitteeMember,
    );

    let votes = vector[validator_address];
    let created_timestamp_ms = clock.timestamp_ms();

    let proposal = Proposal {
        id: object::new(ctx),
        creator: validator_address,
        votes,
        quorum_threshold_bps,
        created_timestamp_ms,
        executed_timestamp_ms: option::none(),
        metadata,
        data,
    };

    let proposal_id = object::id(&proposal);

    // The creator's vote alone can satisfy the threshold (a single-member
    // committee, or one member holding the whole quorum), so check it here
    // the way `vote` does. Skipped before genesis: no committee exists yet,
    // so nothing can reach quorum and there is no committee to weigh against.
    let quorum_reached = has_committee && proposal.quorum_reached(hashi);

    hashi.proposals_mut().active_mut().add(proposal_id, proposal);
    sui::event::emit(ProposalCreated<T> { proposal_id, timestamp_ms: created_timestamp_ms });
    // The creator's vote is recorded above; announce it like any other vote so
    // event consumers can attribute it.
    sui::event::emit(VoteCast<T> { proposal_id, voter: validator_address });
    if (quorum_reached) {
        sui::event::emit(QuorumReached<T> { proposal_id });
    };
    proposal_id
}

public(package) fun execute<T: copy + drop + store>(
    hashi: &mut Hashi,
    proposal_id: ID,
    clock: &Clock,
): T {
    hashi.versioning().assert_version_enabled();

    // Bag membership is the re-execution gate: an already-executed
    // proposal lives only in the executed bag. Check that explicitly so
    // the failure surface is `EProposalAlreadyExecuted` rather than the
    // ObjectBag's generic missing-key abort.
    assert!(!hashi.proposals().executed().contains(proposal_id), EProposalAlreadyExecuted);
    let mut proposal: Proposal<T> = hashi.proposals_mut().active_mut().remove(proposal_id);

    assert!(!proposal.is_expired(clock), EProposalExpired);
    assert!(proposal.quorum_reached(hashi), EQuorumNotReached);

    proposal.executed_timestamp_ms = option::some(clock.timestamp_ms());

    let data = proposal.data;
    let id = proposal.id.to_inner();

    hashi.proposals_mut().executed_mut().add(id, proposal);

    sui::event::emit(ProposalExecuted<T> { proposal_id: id, data });
    data
}

public(package) fun quorum_reached<T>(proposal: &Proposal<T>, hashi: &Hashi): bool {
    let valid_voting_power = proposal.votes.fold!(0, |acc, voter| {
        acc + hashi.current_committee().get_member_weight(&voter)
    });

    let total_weight = hashi.current_committee().total_weight();
    let required = threshold::weight_threshold(total_weight, proposal.quorum_threshold_bps);

    valid_voting_power >= required
}

public(package) fun is_expired<T>(proposal: &Proposal<T>, clock: &Clock): bool {
    clock.timestamp_ms() > proposal.created_timestamp_ms + MAX_PROPOSAL_DURATION_MS
}

public(package) fun delete<T>(proposal: Proposal<T>): T {
    let Proposal<T> {
        id,
        data,
        ..,
    } = proposal;
    id.delete();
    data
}

public(package) fun votes<T>(proposal: &Proposal<T>): &vector<address> {
    &proposal.votes
}

// ~~~~~~~ Test Helpers ~~~~~~~

#[test_only]
public fun data<T>(proposal: &Proposal<T>): &T {
    &proposal.data
}

#[test_only]
public fun vote_cast_for_testing<T>(proposal_id: ID, voter: address): VoteCast<T> {
    VoteCast { proposal_id, voter }
}

#[test_only]
public fun quorum_reached_for_testing<T>(proposal_id: ID): QuorumReached<T> {
    QuorumReached { proposal_id }
}

#[test_only]
public fun proposal_deleted_for_testing<T>(proposal_id: ID): ProposalDeleted<T> {
    ProposalDeleted { proposal_id }
}
