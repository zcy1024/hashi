// Copyright (c) Mysten Labs, Inc.
// SPDX-License-Identifier: Apache-2.0

#[test_only]
module hashi::finish_publish_tests;

use hashi::{hashi::{Self, Hashi}, test_utils};
use sui::coin_registry;

const PUBLISHER: address = @0x1;

fun guardian_btc_public_key(): vector<u8> {
    vector::tabulate!(32, |i| (i as u8))
}

fun registry_for_testing(): coin_registry::CoinRegistry {
    // The registry constructor asserts a system-address sender.
    let ctx = &mut test_utils::new_tx_context(@0x0, 0);
    coin_registry::create_coin_data_registry_for_testing(ctx)
}

#[test]
fun test_finish_publish_stores_upgrade_cap() {
    let ctx = &mut test_utils::new_tx_context(PUBLISHER, 0);
    let mut hashi = test_utils::create_hashi_with_committee(vector[PUBLISHER], ctx);
    let mut registry = registry_for_testing();
    let this_package_id = std::type_name::original_id<Hashi>().to_id();
    let cap = sui::package::test_publish(this_package_id, ctx);

    hashi.finish_publish_for_testing(
        cap,
        @0xb17c,
        b"http://guardian.example:3000".to_string(),
        b"https://node.guardian.example".to_string(),
        guardian_btc_public_key(),
        &mut registry,
        ctx,
    );

    assert!(hashi.versioning().has_upgrade_cap());
    std::unit_test::destroy(hashi);
    std::unit_test::destroy(registry);
}

#[test]
fun test_finish_publish_stores_both_guardian_urls() {
    let ctx = &mut test_utils::new_tx_context(PUBLISHER, 0);
    let mut hashi = test_utils::create_hashi_with_committee(vector[PUBLISHER], ctx);
    let mut registry = registry_for_testing();
    let this_package_id = std::type_name::original_id<Hashi>().to_id();

    hashi.finish_publish_for_testing(
        sui::package::test_publish(this_package_id, ctx),
        @0xb17c,
        b"https://guardian.example".to_string(),
        b"https://node.guardian.example".to_string(),
        guardian_btc_public_key(),
        &mut registry,
        ctx,
    );

    assert!(hashi.config().guardian_url() == option::some(b"https://guardian.example".to_string()));
    assert!(
        hashi.config().guardian_node_url() == option::some(b"https://node.guardian.example".to_string()),
    );
    std::unit_test::destroy(hashi);
    std::unit_test::destroy(registry);
}

#[test]
#[expected_failure(abort_code = hashi::EWrongUpgradeCap)]
fun test_finish_publish_rejects_wrong_cap() {
    let ctx = &mut test_utils::new_tx_context(PUBLISHER, 0);
    let mut hashi = test_utils::create_hashi_with_committee(vector[PUBLISHER], ctx);
    let mut registry = registry_for_testing();
    let cap = sui::package::test_publish(@0xdead.to_id(), ctx);

    hashi.finish_publish_for_testing(
        cap,
        @0xb17c,
        b"http://guardian.example:3000".to_string(),
        b"https://node.guardian.example".to_string(),
        guardian_btc_public_key(),
        &mut registry,
        ctx,
    );

    std::unit_test::destroy(hashi);
    std::unit_test::destroy(registry);
}

#[test]
// The second call aborts inside `set_upgrade_cap` (option::fill on an
// already-filled slot), making the launch call-once.
#[expected_failure]
fun test_finish_publish_twice_aborts() {
    let ctx = &mut test_utils::new_tx_context(PUBLISHER, 0);
    let mut hashi = test_utils::create_hashi_with_committee(vector[PUBLISHER], ctx);
    let mut registry = registry_for_testing();
    let this_package_id = std::type_name::original_id<Hashi>().to_id();

    hashi.finish_publish_for_testing(
        sui::package::test_publish(this_package_id, ctx),
        @0xb17c,
        b"http://guardian.example:3000".to_string(),
        b"https://node.guardian.example".to_string(),
        guardian_btc_public_key(),
        &mut registry,
        ctx,
    );
    hashi.finish_publish_for_testing(
        sui::package::test_publish(this_package_id, ctx),
        @0xb17c,
        b"http://guardian.example:3000".to_string(),
        b"https://node.guardian.example".to_string(),
        guardian_btc_public_key(),
        &mut registry,
        ctx,
    );

    std::unit_test::destroy(hashi);
    std::unit_test::destroy(registry);
}
